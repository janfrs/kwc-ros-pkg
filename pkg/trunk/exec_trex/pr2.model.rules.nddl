/*
 * pr2.rules.nddl
 * Copyright (c) 2008, Willow Garage, Inc.
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *     * Redistributions of source code must retain the above copyright
 *       notice, this list of conditions and the following disclaimer.
 *     * Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *     * Neither the name of the <ORGANIZATION> nor the names of its
 *       contributors may be used to endorse or promote products derived from
 *       this software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 */


/**
 * All behaviors will oscillate between active and inactive
 */
Behavior::Inactive{
  met_by(Active);
  meets(Active);
}

Behavior::Active{
  met_by(Inactive);
  meets(Inactive);
}

/**
 * This will be a decomposition of things to do cleaning. We will require the robot
 * to be at the target position within some error.
 */
CleanBehavior::Active{
  // If the target is bound then generate behavior to accomplish goal
  if(x){
    if(y){	 
      // While cleaning, we expect to be at the desired location.
      contained_by(WaypointController.Inactive wp);
      eq(wp.desX, x);
      eq(wp.desY, y);
    }
  }

  // To be proactive, start the behavior as soon as you can. We do this by generating an action
  // which the agent will start
  starts(Actions.StartBehavior);

  // For now, we will simply do nothing for 10 ticks
  eq(duration, 10);
}

WaypointController::Inactive {
  met_by(Active p);
  eq(p.x, desX);
  eq(p.y, desY);

  meets(Active s);

  // For when we implemenet the controller in NDDL
  if(object.mode == Internal) {
    starts(VehicleState.Holds pos);
    eq(x, pos.x);
    eq(y, pos.y);
  }
}

WaypointController::Active {
  met_by(Inactive p);
  meets(Inactive s);
  eq(x, s.desX);
  eq(y, s.desY);

  if(object.mode == Internal) {
    bool plan_done;
    calcGlobalPath(plan_done, p.x, p.y, x, y);
    starts(VelController.Active vc);
    eq(vc.goal_x, x);
    eq(vc.goal_y, y);
  }
}


/**
 * Implementation for Velocity Controller. Produces Velocity Commands to 
 * bridge gap between current and desired pose. Desired pose is obtained from the WaypointController
 */
VelController::Active {
  // Controller will operate at 1 Hz (cycles per tick)
  eq(duration, 1);
  
  // Bind to prior value
  met_by(VehicleState.Holds currentPos);

  // Bind to the goal source. Could structre this to be more efficient
  // by passing arguments and not requiring reference back to master
  contained_by(WaypointController.Active wp);
  eq(goal_x, wp.x);
  eq(goal_y, wp.y);

  float distanceToGo;
  calcDistance(distanceToGo, currentPos.x, currentPos.y, goal_x, goal_y);
  bool done;
  testLEQ(done, distanceToGo, POSITION_ERROR);
  bool plan_ok;
  if(done == false){
    float cmd_x, cmd_th;
    calcCommand(plan_ok, cmd_x, cmd_th, currentPos.x, currentPos.y,
		currentPos.th, goal_x, goal_y, 0.0);

    if(plan_ok == true){
      if( result == SUCCESS){
	starts(VelCommander.Holds vc);
	eq(vc.cmd_x, cmd_x);
	eq(vc.cmd_th, cmd_th);

	// Pass arguments to successor
	meets(VelController.Active s);
	eq(goal_x,s.goal_x);
	eq(goal_y,s.goal_y);
      }
    }
  }


  /**
   * Any of a number of conditions lead to a transition to the inactive state. We should be able to simple state:
   * if(result == FAILURE || done == true || plan_ok == false){
   *  meets(VelController.Inactive);
   * }
   */

  // First case
  if(result == FAILURE){
    meets(VelController.Inactive);
  }

  // Force termination
  if(done == true) {
    meets(VelController.Inactive);
  }

  if(plan_ok == false){
    meets(VelController.Inactive);
  }
}

VelController::Inactive {
  met_by(Active);
  meets(Active);
  equals(VelCommander.Holds vc);
  eq(vc.cmd_x, 0.0);
  eq(vc.cmd_th, 0.0);

  // Drives transition of waypoint controller state
  equals(WaypointController.Inactive);
}
